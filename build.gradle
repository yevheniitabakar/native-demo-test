plugins {
    id 'java'
    id 'idea'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'com.demo'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url "https://repo.maven.apache.org/maven2/" }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

allure {
    version = '2.27.0'
}

dependencies {
    // Test framework
    testImplementation 'org.testng:testng:7.10.2'

    // Appium client
    implementation 'io.appium:java-client:9.2.2'

    // Selenium WebDriver
    implementation 'org.seleniumhq.selenium:selenium-java:4.16.1'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.12'
    runtimeOnly 'ch.qos.logback:logback-classic:1.5.6'

    // Reporting - Allure
    testImplementation('io.qameta.allure:allure-testng:2.27.0') {
        exclude group: 'io.qameta.allure', module: 'allure-java-commons'
    }
    testImplementation 'io.qameta.allure:allure-java-commons:2.27.0'
    implementation 'io.qameta.allure:allure-java-commons:2.27.0'

    // JSON processing
    implementation 'com.google.code.gson:gson:2.10.1'

    // Utilities
    implementation 'commons-io:commons-io:2.14.0'
}

test {
    def suiteFile = System.getProperty('suiteXmlFile', 'testng.xml')
    def platform = System.getProperty('platform', 'android')

    useTestNG() {
        suites "src/test/resources/${suiteFile}"
    }

    systemProperty 'platform', platform
    systemProperty 'allure.results.directory', "${layout.buildDirectory.asFile.get()}/allure-results"

    jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED']

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('cleanAllureResults', Delete) {
    delete "${layout.buildDirectory.asFile.get()}/allure-results"
    delete "${layout.buildDirectory.asFile.get()}/reports/allure-report"
}


